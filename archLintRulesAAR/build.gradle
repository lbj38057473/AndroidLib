apply plugin: 'com.android.library'
project.archivesBaseName = 'common-arch-lint-rules'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

/*
 * rules for including "lint.jar" in aar
 */
configurations {
    lintJarImport
}

dependencies {
    lintJarImport project(path: ":archLintRules", configuration: "lintJarOutput")
}

task copyLintJar(type: Copy) {
    from (configurations.lintJarImport) {
        rename {
            String fileName ->
                'lint.jar'
        }
    }
    into 'build/intermediates/lint/'
}

project.afterEvaluate {
    def compileLintTask = project.tasks.find { it.name == 'compileLint' }
    compileLintTask.dependsOn(copyLintJar)
}

project.ext {
    moduleName = 'me.ycdev.android.common-arch-lint-rules'
    moduleDesc = 'Lint rules for common arch module in AndroidLib project'
}

apply from: rootProject.file('bintray-install.gradle')
apply from: rootProject.file('bintray-upload.gradle')
